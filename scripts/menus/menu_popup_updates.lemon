// The main function is control management in the menu.
function void AchievementsPlus.Menu.popup.updateControl()
{
	// Controls in the Options menu
	if (achievementsplus.popupMenu.tempValue1 == POPUP_ACHIEVEMENTS_OPTIONS)
	{
		// Close popup
		if (control.pad1.pressed & CONTROL_B)
			AchievementsPlus.Menu.popup.close()
		
		// Go to confirmation menu
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			AchievementsPlus.Menu.popup.show(POPUP_OPTIONS_YOU_ARE_SURE, "One moment...")
		}
		
		// Select a button in the menu
		if (control.pad1.pressed & (CONTROL_UP | CONTROL_DOWN))
		{
			playSound(0x5b)
			if (control.pad1.pressed & CONTROL_UP)
			{
				if (achievementsplus.popupMenu.tempValue5 != 0x00)
					--achievementsplus.popupMenu.tempValue5
				else
					achievementsplus.popupMenu.tempValue5 = 0x01
			}
			else if (control.pad1.pressed & CONTROL_DOWN)
			{
				if (achievementsplus.popupMenu.tempValue5 < 0x01)
					++achievementsplus.popupMenu.tempValue5
				else
					achievementsplus.popupMenu.tempValue5 = 0x00
			}
		}  
		
		// A carousel is available for the first button
		if (achievementsplus.popupMenu.tempValue5 == 0x00)
		{
			// Select a carousel button in the menu
			if (control.pad1.pressed & (CONTROL_LEFT | CONTROL_RIGHT))
			{
				playSound(0x5b)
				if (control.pad1.pressed & CONTROL_LEFT)
				{
					if (achievementsplus.popupMenu.tempValue6 != 0x00)
						--achievementsplus.popupMenu.tempValue6
					else
						achievementsplus.popupMenu.tempValue6 = 0x02
				}
				else if (control.pad1.pressed & CONTROL_RIGHT)
				{
					if (achievementsplus.popupMenu.tempValue6 < 0x02)
						++achievementsplus.popupMenu.tempValue6
					else
						achievementsplus.popupMenu.tempValue6 = 0x00
				}
			} 
		}
		
		// Finish the function on this
		return
	}
	
	// Control for the confirmation menu
	else if (achievementsplus.popupMenu.tempValue1 == POPUP_OPTIONS_YOU_ARE_SURE)
	{
		// Close popup (Actually, back to the Options menu)
		if (control.pad1.pressed & CONTROL_B)
			AchievementsPlus.Menu.popup.show(POPUP_ACHIEVEMENTS_OPTIONS, "Options")
		
		// Perform an action
		if (control.pad1.pressed & CONTROL_START)
		{
			playSound(0x63)
			
			if (achievementsplus.popupMenu.tempValue5 == 0x00)
			{
				if (achievementsplus.popupMenu.tempValue6 == 0x00)
					AchievementsPlus.AchivementsManager.cleanPersistentDataByRange(ACHIEVEMENTS_START_ID[0x00], ACHIEVEMENTS_LAST_ID[0x02], false)
				else if (achievementsplus.popupMenu.tempValue6 == 0x01)
					AchievementsPlus.AchivementsManager.cleanPersistentDataByRange(ACHIEVEMENTS_START_ID[0x01], ACHIEVEMENTS_LAST_ID[0x01], false)
				else if (achievementsplus.popupMenu.tempValue6 == 0x02)
					AchievementsPlus.AchivementsManager.cleanPersistentDataByRange(ACHIEVEMENTS_START_ID[0x02], ACHIEVEMENTS_LAST_ID[0x02], false)
			}
			else if (achievementsplus.popupMenu.tempValue5 == 0x01)
			{
				AchievementsPlus.RewardsManager.cleanPersistendData()
				AchievementsPlus.AchivementsManager.cleanPersistentDataByRange(ACHIEVEMENTS_START_ID[0x00], ACHIEVEMENTS_LAST_ID[0x02], true)
			}
			
			// Go to Data Select menu
			AchievementsPlus.Menu.achievements.setupTabs_rewardsAvailable()
			global.game_mode = 0x4c
			
			// Close popup menu
			AchievementsPlus.Menu.popup.close()
		}
		
		// Finish the function on this
		return
	}
	
	// Control for the Debug mode is enabled and Game via Level select menu
	else if (achievementsplus.popupMenu.tempValue1 == POPUP_VIA_LEVEL_SELECT)
	{
		// Close popup
		if (control.pad1.pressed & CONTROL_START)
		{
			AchievementsPlus.Menu.popup.close()
			achievementsplus.popupMenu.tempValue7 |= 0x100
		}
		
		// Finish the function on this
		return
	}
	
	// Control for the Debug mode enabled menu
	else if (achievementsplus.popupMenu.tempValue1 == POPUP_DEBUG_MODE_ENABLED)
	{
		// Close popup (Actually, back to the Main menu)
		if (control.pad1.pressed & CONTROL_B)
		{
			Game.returnToMainMenu()
			AchievementsPlus.Menu.popup.close()
		}
		
		// Close popup
		if (control.pad1.pressed & CONTROL_START)
		{
			AchievementsPlus.Menu.popup.close()
			achievementsplus.popupMenu.tempValue7 |= 0x1000
		}
		
		// Finish the function on this
		return
	}
	
	// Failsave close popup
	if (control.pad1.pressed & CONTROL_B)
	{
		AchievementsPlus.Menu.popup.close()
	}
}


// The main function of the update.
function void AchievementsPlus.Menu.popup.update()
{
	if (!achievementsplus.popupMenu.tempValue4)
		return
	
	syncZ80_variantB(0x01)
	AchievementsPlus.Menu.popup.setup()
	syncZ80_variantB(0x80)
	
	achievementsplus.popupMenu.tempValue4 = 0x00
}


// The function defines the ID and title for the popup and shows it.
function void AchievementsPlus.Menu.popup.show(u8 id, string title)
{
	achievementsplus.popupMenu.tempValue2 = 0x00
	achievementsplus.popupMenu.tempValue3 = 0x00
	
	achievementsplus.popupMenu.tempValue0 = title
	achievementsplus.popupMenu.tempValue1 = id
	
	achievementsplus.popupMenu.tempValue4 = 0x01
}


// The function resets some variables and closes popup.
function void AchievementsPlus.Menu.popup.close()
{
	achievementsplus.popupMenu.tempValue4 = 0x00
	
	//achievementsplus.popupMenu.tempValue0 = ""
	//achievementsplus.popupMenu.tempValue1 = 0x00
}